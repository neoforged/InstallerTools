import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import net.neoforged.gradleutils.PomUtilsExtension.License

plugins {
  id 'net.neoforged.licenser'
  id 'com.gradleup.shadow' apply false
  id 'net.neoforged.gradleutils' apply false
}

apply plugin : ProjectDefaultsPlugin
apply plugin : CliToolPlugin

gradleutils {
    setupSigning()
    setupCentralPublishing()
}

base {
    archivesName = 'installertools'
}
application {
    mainClass = 'net.neoforged.installertools.ConsoleTool'
}

license {
  header project.file('LICENSE-header.txt')
  include 'net/neoforged/installertools/**/*.java'
  newLine false
}

dependencies {
    implementation(libs.srgutils)
    implementation(libs.jopt)
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'de.siegmar:fastcsv:2.0.0'
    implementation(libs.asm.commons)
    implementation project(':cli-utils')

    testImplementation(libs.bundles.junit)
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = base.archivesName.get()

            pom {
                name = 'Installer Tools'
                description = 'A collection of command line tools that are useful for the Forge installer, that are not worth being their own standalone projects.'
            }
        }
    }
}

allprojects {
    publishing {
        publications {
            withType(MavenPublication).configureEach {
                it.pom {
                    pomUtils.githubRepo(it, 'InstallerTools')
                    pomUtils.license(it, License.LGPL_v2)
                    pomUtils.neoForgedDeveloper(it)
                }
            }
        }
        repositories {
            maven gradleutils.publishingMaven
        }
    }
}
